/*
 * Copyright (c) 2018 David Aguiar Gonzalez
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
	repositories {
		maven { url 'https://maven.fabric.io/public' }
		mavenCentral()
	}

	dependencies {
		classpath 'io.fabric.tools:gradle:1.31.2'
	}
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

repositories {
	maven { url 'https://maven.fabric.io/public' }
	mavenCentral()
}

android {
	compileSdkVersion 28
	buildToolsVersion '28.0.3'

	defaultConfig {
		applicationId 'gc.david.dfm'
		minSdkVersion 15
		targetSdkVersion 28
		versionName '3.4'
		versionCode 26
		resConfigs "en", "ca", "de", "es", "fr", "it", "pt"
	}

	buildTypes {
		debug {
			versionNameSuffix "-dev"

			// Uncomment this to test ProGuard before release
//			minifyEnabled true
//			shrinkResources true
//			proguardFiles getDefaultProguardFile('proguard-android.txt')
//			proguardFiles fileTree(dir: "proguard", include: ["*.pro"]).asList().toArray()

			if (System.getenv("TRAVIS")) {
				resValue "string", "maps_debug_api_key", "guess_it"
				resValue "string", "maps_release_api_key", "guess_it"
				resValue "string", "maps_geocode_api_key", "guess_it"
			}
		}
		release {
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android.txt')
			proguardFiles fileTree(dir: "proguard", include: ["*.pro"]).asList().toArray()
		}
		beta {
			initWith buildTypes.release

			versionNameSuffix "-beta"
		}
	}
}

ext {
	supportLibraryGroup = 'com.android.support'
	playServicesGroup = 'com.google.android.gms'
	butterKnifeGroup = 'com.jakewharton'
	daggerGroup = 'com.google.dagger'
	leakCanaryGroup = 'com.squareup.leakcanary'

	supportLibraryVersion = '27.1.1'
	playServicesVersion = '15.0.1'
	graphViewVersion = '3.1.3'
	butterKnifeVersion = '8.8.1'
	greenDaoVersion = '3.2.2'
	crashlyticsVersion = '2.9.5'
	daggerVersion = '2.16'
	okhttpVersion = '3.11.0'
	gsonVersion = '2.8.5'
	constraintLayoutVersion = '1.1.3'
	leakCanaryVersion = '1.6.1'

	junitVersion = '4.12'
	mockitoVersion = '2.3.0'
}

dependencies {
	implementation "$supportLibraryGroup:design:$supportLibraryVersion"
	implementation("$supportLibraryGroup:preference-v7:$supportLibraryVersion") {
		transitive = false
	}
	implementation("$playServicesGroup:play-services-maps:$playServicesVersion") {
		exclude group: "com.android.support", module: "support-v4"
	}
	implementation("$playServicesGroup:play-services-location:$playServicesVersion") {
		transitive = false
	}
	implementation("com.jjoe64:graphview:$graphViewVersion") {
		transitive = false
	}
	implementation("$butterKnifeGroup:butterknife:$butterKnifeVersion") {
		exclude group: "com.android.support", module: "support-annotations"
	}
	kapt "$butterKnifeGroup:butterknife-compiler:$butterKnifeVersion"
	implementation "org.greenrobot:greendao:$greenDaoVersion"
	implementation("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion@aar") {
		transitive = true
	}
	implementation "$daggerGroup:dagger:$daggerVersion"
	kapt "$daggerGroup:dagger-compiler:$daggerVersion"
	implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
	implementation "com.google.code.gson:gson:$gsonVersion"
	implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"
	debugImplementation "$leakCanaryGroup:leakcanary-android:$leakCanaryVersion"
	betaImplementation "$leakCanaryGroup:leakcanary-android-no-op:$leakCanaryVersion"
	releaseImplementation "$leakCanaryGroup:leakcanary-android-no-op:$leakCanaryVersion"
	implementation("$supportLibraryGroup:cardview-v7:$supportLibraryVersion") {
		exclude group: "com.android.support", module: "support-annotations"
	}

	testImplementation "junit:junit:$junitVersion"
	testImplementation "org.mockito:mockito-core:$mockitoVersion"
	testImplementation "$leakCanaryGroup:leakcanary-android-no-op:$leakCanaryVersion"
}

