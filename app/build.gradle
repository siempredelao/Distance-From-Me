/*
 * Copyright (c) 2017 David Aguiar Gonzalez
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
	repositories {
		maven { url 'https://maven.fabric.io/public' }
		mavenCentral()
	}

	dependencies {
		classpath 'io.fabric.tools:gradle:1.+'
	}
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'

repositories {
	maven { url 'https://maven.fabric.io/public' }
	mavenCentral()
}

android {
	compileSdkVersion 25
	buildToolsVersion '25.0.0'

	defaultConfig {
		applicationId 'gc.david.dfm'
		minSdkVersion 15
		targetSdkVersion 25
		versionName '3.2.1'
		versionCode 24
		resConfigs "en", "ca", "de", "es", "fr", "it", "pt"
	}

	buildTypes {
		debug {
			versionNameSuffix "-dev"

			// Uncomment this to test ProGuard before release
//			minifyEnabled true
//			shrinkResources true
//			proguardFiles getDefaultProguardFile('proguard-android.txt')
//			proguardFiles fileTree(dir: "proguard", include: ["*.pro"]).asList().toArray()

			if (System.getenv("TRAVIS")) {
				resValue "string", "maps_debug_api_key", "guess_it"
				resValue "string", "maps_release_api_key", "guess_it"
				resValue "string", "maps_geocode_api_key", "guess_it"
				resValue "string", "inmobi_api_key", "guess_it"
			}
		}
		release {
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android.txt')
			proguardFiles fileTree(dir: "proguard", include: ["*.pro"]).asList().toArray()
		}
		beta {
			initWith buildTypes.release

			versionNameSuffix "-beta"
		}
	}
}

ext {
	supportLibraryGroup = 'com.android.support'
	playServicesGroup = 'com.google.android.gms'
	butterKnifeGroup = 'com.jakewharton'
	daggerGroup = 'com.google.dagger'
	leakCanaryGroup = 'com.squareup.leakcanary'

	supportLibraryVersion = '25.0.1'
	playServicesVersion = '9.8.0'
	graphViewVersion = '3.1.3'
	inmobiVersion = '6.0.3'
	butterKnifeVersion = '8.4.0'
	greenDaoVersion = '3.2.0'
	crashlyticsVersion = '2.6.5'
	daggerVersion = '2.8'
	okhttpVersion = '3.5.0'
	gsonVersion = '2.8.0'
	constraintLayoutVersion = '1.0.0-beta4'
	leakCanaryVersion = '1.5'

	junitVersion = '4.12'
	mockitoVersion = '2.3.0'
}

dependencies {
	compile "$supportLibraryGroup:design:$supportLibraryVersion"
	compile("$supportLibraryGroup:preference-v7:$supportLibraryVersion") {
		transitive = false;
	}
	compile("$playServicesGroup:play-services-maps:$playServicesVersion") {
		exclude group: "com.android.support", module: "support-v4"
	}
	compile("$playServicesGroup:play-services-location:$playServicesVersion") {
		transitive = false;
	}
	compile("com.jjoe64:graphview:$graphViewVersion") {
		transitive = false;
	}
	compile "com.inmobi.monetization:inmobi-ads:$inmobiVersion"
	compile("$butterKnifeGroup:butterknife:$butterKnifeVersion") {
		exclude group: "com.android.support", module: "support-annotations"
	}
	annotationProcessor "$butterKnifeGroup:butterknife-compiler:$butterKnifeVersion"
	compile "org.greenrobot:greendao:$greenDaoVersion"
	compile("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion@aar") {
		transitive = true;
	}
	compile "$daggerGroup:dagger:$daggerVersion"
	annotationProcessor "$daggerGroup:dagger-compiler:$daggerVersion"
	compile "com.squareup.okhttp3:okhttp:$okhttpVersion"
	compile "com.google.code.gson:gson:$gsonVersion"
	compile "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"
	debugCompile "$leakCanaryGroup:leakcanary-android:$leakCanaryVersion"
	betaCompile "$leakCanaryGroup:leakcanary-android-no-op:$leakCanaryVersion"
	releaseCompile "$leakCanaryGroup:leakcanary-android-no-op:$leakCanaryVersion"
	compile("$supportLibraryGroup:cardview-v7:$supportLibraryVersion") {
		exclude group: "com.android.support", module: "support-annotations"
	}

	testCompile "junit:junit:$junitVersion"
	testCompile "org.mockito:mockito-core:$mockitoVersion"
	testCompile  "$leakCanaryGroup:leakcanary-android-no-op:$leakCanaryVersion"
}

