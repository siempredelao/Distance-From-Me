/*
 * Copyright (c) 2018 David Aguiar Gonzalez
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
	repositories {
		maven { url 'https://maven.fabric.io/public' }
		mavenCentral()
	}

	dependencies {
		classpath 'io.fabric.tools:gradle:1.31.2'
	}
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

repositories {
	maven { url 'https://maven.fabric.io/public' }
	mavenCentral()
}

// Needed to allow Parcelize
androidExtensions {
	experimental = true
}

android {
	compileSdkVersion 28
	buildToolsVersion '28.0.3'

	defaultConfig {
		applicationId 'gc.david.dfm'
		minSdkVersion 22
		targetSdkVersion 28
		versionName '3.4'
		versionCode 26
		resConfigs "en", "ca", "de", "es", "fr", "it", "pt"
	}

	buildTypes {
		debug {
			versionNameSuffix "-dev"

			// Uncomment this to test ProGuard before release
//			minifyEnabled true
//			shrinkResources true
//			proguardFiles getDefaultProguardFile('proguard-android.txt')
//			proguardFiles fileTree(dir: "proguard", include: ["*.pro"]).asList().toArray()

			if (System.getenv("TRAVIS")) {
				resValue "string", "maps_debug_api_key", "guess_it"
				resValue "string", "maps_release_api_key", "guess_it"
				resValue "string", "maps_geocode_api_key", "guess_it"
			}
		}
		release {
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android.txt')
			proguardFiles fileTree(dir: "proguard", include: ["*.pro"]).asList().toArray()
		}
		beta {
			initWith buildTypes.release

			versionNameSuffix "-beta"
		}
	}

	// Butterknife requires Java 8
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

ext {
	playServicesGroup = 'com.google.android.gms'
	butterKnifeGroup = 'com.jakewharton'
	daggerGroup = 'com.google.dagger'
	leakCanaryGroup = 'com.squareup.leakcanary'

	playServicesVersion = '17.0.0'
	graphViewVersion = '3.1.3'
	butterKnifeVersion = '10.2.0'
	greenDaoVersion = '3.2.2'
	crashlyticsVersion = '2.9.5'
	daggerVersion = '2.16'
	okhttpVersion = '3.11.0'
	gsonVersion = '2.8.6'
	leakCanaryVersion = '1.6.3'

	junitVersion = '4.12'
	mockitoVersion = '2.28.2'
}

dependencies {
	implementation "com.google.android.material:material:1.0.0"
	implementation("androidx.preference:preference:1.0.0") {
		transitive = false
	}
	implementation("$playServicesGroup:play-services-maps:$playServicesVersion") {
		exclude group: "androidx.legacy", module: "legacy-support-v4"
	}
	implementation("$playServicesGroup:play-services-location:$playServicesVersion") {
		transitive = false
	}
	implementation("com.jjoe64:graphview:$graphViewVersion") {
		transitive = false
	}
	implementation("$butterKnifeGroup:butterknife:$butterKnifeVersion") {
		exclude group: "androidx.annotation", module: "annotation"
	}
	kapt "$butterKnifeGroup:butterknife-compiler:$butterKnifeVersion"
	implementation "org.greenrobot:greendao:$greenDaoVersion"
	implementation("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion@aar") {
		transitive = true
	}
	implementation "$daggerGroup:dagger:$daggerVersion"
	kapt "$daggerGroup:dagger-compiler:$daggerVersion"
	implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
	implementation "com.google.code.gson:gson:$gsonVersion"
	implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
	debugImplementation "$leakCanaryGroup:leakcanary-android:$leakCanaryVersion"
	betaImplementation "$leakCanaryGroup:leakcanary-android-no-op:$leakCanaryVersion"
	releaseImplementation "$leakCanaryGroup:leakcanary-android-no-op:$leakCanaryVersion"
	implementation('androidx.cardview:cardview:1.0.0') {
		exclude group: "androidx.annotation", module: "annotation"
	}
	implementation("androidx.collection:collection-ktx:1.1.0")
	implementation("androidx.core:core-ktx:1.1.0")

	testImplementation "junit:junit:$junitVersion"
	testImplementation "org.mockito:mockito-core:$mockitoVersion"
	testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
	testImplementation "$leakCanaryGroup:leakcanary-android-no-op:$leakCanaryVersion"
	testImplementation 'org.mockito:mockito-inline:2.22.0'
}

